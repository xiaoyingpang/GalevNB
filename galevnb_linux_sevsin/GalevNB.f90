!!$***********************************************************
!!$
!!$  Parser NBODY6 -> Galev
!!$
!!$  written by C. Olczak 2012/13
!!$  based on a draft of C. Euler from 2012
!!$
!!$***********************************************************

PROGRAM specGalev

  IMPLICIT NONE

  ! Defining constants.
 !CHARACTER( LEN = * ), PARAMETER :: shell__find_snapshots      = "find . -maxdepth 1 -regex '\.\/snap\.40_[0-9]*\.h5part_Step#[0-9]*_s\.csv' | cut -c 3-"
  CHARACTER( LEN = * ), PARAMETER :: shell__find_snapshots      = "find . -maxdepth 1 -regex '\.\/sev\.83_[0-9]*' | cut -c 3-"

  CHARACTER( LEN = * ), PARAMETER :: script__generate_snapshots = "generate_snapshots.sh"

  CHARACTER( LEN = * ), PARAMETER :: filename__snapshots_main   = "sev.*"
  CHARACTER( LEN = * ), PARAMETER :: filename__snapshots_suffix = "sev.*"


  CHARACTER( LEN = * ), PARAMETER :: filename__snapshot_list   = "snapshot_files.lst"
  INTEGER             , PARAMETER :: unit__snapshot_list       = 10

  INTEGER             , PARAMETER :: unit__snaphot             = 11
  INTEGER             , PARAMETER :: unit__stellar_magnitudes  = 12

  CHARACTER( LEN = * ), PARAMETER :: filename__cluster_int_mag = "cluster_int_mag.dat"
  INTEGER             , PARAMETER :: unit__cluster_int_mag     = 13


  ! Declarations
  CHARACTER( LEN = 64 ) :: filename__snapshot
  CHARACTER( LEN = 64 ) :: filename__stellar_magnitudes
  CHARACTER( LEN = 22 ) :: filename__cluster_spectra

  REAL*8  :: time, particle_num ! time.

  REAL*8 :: temp, mass, lum, zmet ! Effective temperature, stellar mass, bolometric luminosity, metallicity
  REAL*8 :: dummy
  INTEGER :: particle_index
  INTEGER :: stellar_type ! stellar type is defined in file: "define.h"

  REAL*8, DIMENSION(:), ALLOCATABLE :: stellar_mags 
  REAL*8, DIMENSION(:), ALLOCATABLE :: integrated_mags

  INTEGER              :: number_of_filters
  CHARACTER( LEN = 2 ) :: number_of_filters__string

  INTEGER              :: snapshot_counter
  CHARACTER( LEN = 15 ) :: snapshot_string

  INTEGER :: read_status
  INTEGER :: k



  ! Initialize GALEV routines.
  CALL startomaginit( number_of_filters )
  CALL specint_initialize


  ! Transfer number of filters to string.
  WRITE( UNIT = number_of_filters__string, FMT = "( i2 )" ) number_of_filters

  ! Allocate arrays with known number of filters.
  ALLOCATE( stellar_mags( number_of_filters ) )
  ALLOCATE( integrated_mags( number_of_filters ) )

  ! Initialize.
  stellar_mags    = 0.0
  integrated_mags = 0.0

  ! Open file for writing cluster integrated magnitudes.
  !OPEN( UNIT = unit__cluster_int_mag, FILE = TRIM( filename__cluster_int_mag ), STATUS = "REPLACE", ACTION = "WRITE", ACCESS = "APPEND" )
  !WRITE( UNIT = unit__cluster_int_mag, FMT = "( a1, 1x, a9, 2x, a15 )" ) "#", "time", "integrated_mags"
  !WRITE( UNIT = unit__cluster_int_mag, FMT = "( a1, 1x, a9, 2x, "//number_of_filters__string//"( 1x, a6 ) )" ) "#", "[Myr]", ( "[mag]", k = 1, number_of_filters )

  ! Inform user.
  WRITE( UNIT = 6, FMT = "( a )" ) "Creating file for cluster integrated magnitudes: """//TRIM( filename__cluster_int_mag )//"""."


  ! Build list of snapshot files.
  CALL system( TRIM( shell__find_snapshots )//" > "//TRIM( filename__snapshot_list ) )

  ! Access list of snapshot files.
  OPEN( UNIT = unit__snapshot_list, FILE = TRIM( filename__snapshot_list ) )

  ! Initialize snapshot counter.
  snapshot_counter = 0

  ! Run loop over snapshot files.
  loop__snapshots: DO

     ! Get next snapshot.
     READ( UNIT = unit__snapshot_list, FMT = *, IOSTAT = read_status ) filename__snapshot

     ! Check for end of list.
     check__read_snapshot_list: IF ( read_status < 0 ) THEN

        ! Check for missing files.
        IF ( snapshot_counter == 0 ) THEN
           WRITE( UNIT = 6, FMT = * )
           WRITE( UNIT = 6, FMT = "( a )" ) "ERROR: snapshot file list """//TRIM( filename__snapshot_list )//""" is empty."
           WRITE( UNIT = 6, FMT = * )
           WRITE( UNIT = 6, FMT = "( a )" ) "(NOTE: The file list is generated by executing the shell command """//TRIM( shell__find_snapshots )//""")"
           WRITE( UNIT = 6, FMT = * )
           WRITE( UNIT = 6, FMT = "( a )" ) "EXPLANATION: This means the required snapshot files """//TRIM( filename__snapshots_suffix )//""" are not present in the current directory."
           WRITE( UNIT = 6, FMT = * )
           WRITE( UNIT = 6, FMT = "( a )" ) "SOLUTIONS:"
           WRITE( UNIT = 6, FMT = "( a )" ) "1. Move to the directory where the snapshot files are present and re-run."
           WRITE( UNIT = 6, FMT = "( a )" ) "2. If there are no snapshot files at all but the NBODY6 output file """//TRIM( filename__snapshots_main )//""" exists"
           WRITE( UNIT = 6, FMT = "( a )" ) "   generate the snapshots by running the shell script """//TRIM( script__generate_snapshots )//""" located in the subdirectory ""scripts"" and re-run."
           WRITE( UNIT = 6, FMT = * )
           WRITE( UNIT = 6, FMT = "( a )" ) "STOPPED."
           WRITE( UNIT = 6, FMT = * )
           STOP
        END IF

        ! List processed successfully.
        EXIT loop__snapshots

     ELSE IF ( read_status > 0 ) THEN

        ! Encountered read error: print information for user.
        WRITE( UNIT = 6, FMT = "( a )" ) "READ ERROR occured in file """//TRIM( filename__snapshot_list )//""" for file "//TRIM( filename__snapshot )//"."
        STOP

     END IF check__read_snapshot_list


     ! Open snapshot file with stellar parameters.
     OPEN( UNIT = unit__snaphot, FILE = TRIM( filename__snapshot ), STATUS = "OLD", ACTION = "READ" ) ! TRIM() is used to trim the emtpy spaces
     ! Inform user.
     WRITE( UNIT = 6, FMT = "( a )" ) "Opening file with stellar parameters: """//TRIM( filename__snapshot )//"""."


     ! Reset current integrated spectrum in GALEV (to be done before calculating a new spectrum).
     CALL reset_weights()

     ! Read header line, NBODY time:
     READ( UNIT = unit__snaphot, FMT = *, IOSTAT = read_status ) particle_num, time
     ! Check read status for end of file or read error.
     check__read_snapshot_header: IF ( read_status < 0 ) THEN

        ! Close file for output of stellar magnitudes.
        CLOSE( UNIT = unit__stellar_magnitudes )

        ! Close snapshot file.
        CLOSE( UNIT = unit__snaphot )

        ! Reached end of file: exit loop.
        EXIT loop__snapshots

     ELSE IF ( read_status > 0 ) THEN

        ! Encountered read error: print information for user.
        !WRITE( UNIT = 6, FMT = "( a, f11.4, a )" ) "READ ERROR occured in file """//TRIM( filename__snapshot )//""" when reading header file with time ", time, ", "
                ! Close file for output of stellar magnitudes.
        CLOSE( UNIT = unit__stellar_magnitudes )

        ! Close snapshot file.
        CLOSE( UNIT = unit__snaphot )

        ! Terminate program.
        STOP

     END IF check__read_snapshot_header


     ! Open file for output of stellar magnitudes.
     !WRITE( UNIT = snapshot_string, FMT = "( i4.4 )" ) snapshot_counter

     WRITE( UNIT = snapshot_string, FMT = "( f11.4 )" ) time
     filename__stellar_magnitudes = "stellar_magnitudes-"//TRIM(adjustl( snapshot_string ))//"_s"
     OPEN( UNIT = unit__stellar_magnitudes, FILE = TRIM( filename__stellar_magnitudes ), ACTION = "WRITE", STATUS = "REPLACE" )

     ! Write header information.
     WRITE( UNIT = unit__stellar_magnitudes, FMT = "( a1, 1x, a9 )" ) "#","time_Myr"
     WRITE( UNIT = unit__stellar_magnitudes, FMT = "( f11.4 )" ) time
     WRITE( UNIT = unit__stellar_magnitudes, FMT = "( a1, a6, 1x, a4, 2x, a8, 2x, a8, 2x, a8, 2x, a7, 2x, a10 )" ) "#", "id", "type", "mass", "lum", "temp", "zmet", "magnitudes"
     WRITE( UNIT = unit__stellar_magnitudes, FMT = "( a1, a6, 1x, a4, 2x, a8, 2x, a8, 2x, a8, 2x, a7, 2x, "//number_of_filters__string//"( 1x, a6 ) )" ) "#", "", "", "[Msun]", "[Lsun]", "[K]", "", ( "[mag]", k = 1, number_of_filters )


     ! Loop over all stars in given time step block.
     loop__stars: DO

        ! Read stellar parameters.
        ! NOTE: Quantities are given as follows:
        ! - temp in log of Kelvin [K]
        ! - mass in solar masses [Msun]
        ! - lum in log of solar luminosity [Lsun]
        ! - metallicity in mass fraction (~0.02 is solar) (not log!)

        READ( UNIT = unit__snaphot, FMT = *, IOSTAT = read_status ) dummy, dummy, particle_index, stellar_type, dummy, mass, lum, dummy, temp ! NOTE: metallicity zmet shall be an individual stellar read-in parameter.

        ! Check read status for end of file or read error.
        check__read_snapshot_file: IF ( read_status < 0 ) THEN

           ! Close file for output of stellar magnitudes.
           CLOSE( UNIT = unit__stellar_magnitudes )

           ! Reached end of file: exit loop.
           EXIT loop__stars

        ELSE IF ( read_status > 0 ) THEN

           ! Encountered read error: print information for user.
           WRITE( UNIT = 6, FMT = "( a, f11.4, a )" ) "READ ERROR occured in file """//TRIM( filename__snapshot )//""" for particle ", time, "."
           STOP

        END IF check__read_snapshot_file

        ! NOTE: Providing fixed metallicity: here for testing.
        zmet = 0 ! [Fe/H]
        !zmet = -0.56 ! [Fe/H]

        ! Calculate stellar magnitude via GALEV.
        ! NOTE: Provide
        ! - temp in Kelvin [K] (not log!)
        ! - mass in solar masses [Msun]
        ! - lum in solar luminosity [Lsun] (not log!)
        ! - metallicity (0.0 is solar) (log!)
        CALL startomag( 10**temp, mass, 10**lum, zmet, stellar_mags )

        ! Output of stellar magnitudes to file.
        ! - particle index from simulation
        ! - stellar type number code from simulation
        ! - mass in solar masses [Msun]
        ! - temp in Kelvin [K] (not log!)
        ! - lum in solar luminosity [Lsun] (not log!)
        ! - metallicity (0.0 is solar) (log!)
        ! - magnitude in [mag]
        WRITE( UNIT = unit__stellar_magnitudes, FMT = "( i7, 3x, i2, 2x, f8.3, 2x, es12.5, 2x, es12.5, 2x, f9.5, 2x, "//number_of_filters__string//"( 1x, f6.2 ) )" ) &
             particle_index, stellar_type, mass,10**lum, 10**temp, zmet, stellar_mags
        ! Construct integrated spectrum via GALEV.
        ! NOTE: Provide
        ! - temp in Kelvin [K] (not log!)
        ! - mass in solar masses [Msun]
        ! - lum in in solar luminosity [Lsun] (not log!)
        ! - metallicity in mass fraction (~0.02 is solar) (not log!)

        ! Exclude dark remnants:
        ! - neutron star: stellar_type = 13 (Nbody6)
        ! - black hole:   stellar_type = 14 (Nbody6)
        check__remnants: IF ( stellar_type /= 13 .AND. stellar_type /= 14 ) THEN
           CALL add_star( 10**temp, mass, 10**lum, zmet )
        END IF check__remnants

     END DO loop__stars

     ! Inform user.
     WRITE( UNIT = 6, FMT = "( a )" ) "Stellar magnitudes written to file """//TRIM( filename__stellar_magnitudes )//"""."


     ! Compute magnitudes from integrated spectrum via GALEV.
     CALL spec2mag( integrated_mags )

     ! Write cluster integrated magnitude.
     WRITE( UNIT = unit__cluster_int_mag, FMT = "( f11.4, 2x, "//number_of_filters__string//"( 1x, f6.2 ) )" ) time, integrated_mags
     CALL flush( UNIT = unit__cluster_int_mag )
     ! Inform user.
     WRITE( UNIT = 6, FMT = "( 1x, a )" ) "Adding integrated cluster magnitude to file """//TRIM( filename__cluster_int_mag )//"""."

     ! Dump current integrated spectrum via GALEV (file 'spec_out') and store for given time step.
     filename__cluster_spectra = "spec_out-"//TRIM(adjustl( snapshot_string ))//"_s"
     CALL spec_output( filename__cluster_spectra )
     WRITE( UNIT = 6, FMT = "( 1x, a )" ) "Integrated spectrum written to file """//TRIM( filename__cluster_spectra )//"""."

     ! Increase snapshot counter.
     snapshot_counter = snapshot_counter + 1


  END DO loop__snapshots


  ! Close file with time sequence of integrated cluster magnitudes.
  CLOSE( UNIT = unit__cluster_int_mag, STATUS = "keep" )

  ! Close file containing all snapshots with stellar parameters.
  CLOSE( UNIT = unit__snapshot_list, STATUS = "keep" )


END PROGRAM specGalev
